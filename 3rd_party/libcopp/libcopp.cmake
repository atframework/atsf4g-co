if (CMAKE_VERSION VERSION_GREATER_EQUAL "3.10")
    include_guard(GLOBAL)
endif()

# =========== 3rdparty libcopp ==================
set (3RD_PARTY_LIBCOPP_BASE_DIR ${CMAKE_CURRENT_LIST_DIR})
set (3RD_PARTY_LIBCOPP_PKG_DIR "${CMAKE_CURRENT_LIST_DIR}/pkg")
set (3RD_PARTY_LIBCOPP_REPO_DIR "${CMAKE_CURRENT_LIST_DIR}/repo")

if(LIBCOPP_ROOT)
    set (3RD_PARTY_LIBCOPP_ROOT_DIR ${LIBCOPP_ROOT})
else()
    set (3RD_PARTY_LIBCOPP_ROOT_DIR ${3RD_PARTY_LIBCOPP_REPO_DIR})
endif()

find_package(Libcopp)
if (Libcopp_FOUND)
    set (3RD_PARTY_LIBCOPP_INC_DIR ${Libcopp_INCLUDE_DIRS})
    set (3RD_PARTY_LIBCOPP_LINK_NAME ${Libcopp_LIBRARIES} ${Libcotask_LIBRARIES})

    EchoWithColor(COLOR GREEN "-- Dependency: libcopp prebuilt found.(inc=${Libcopp_INCLUDE_DIRS})")
elseif(EXISTS "${3RD_PARTY_LIBCOPP_REPO_DIR}/CMakeLists.txt")
    set (3RD_PARTY_LIBCOPP_INC_DIR "${3RD_PARTY_LIBCOPP_REPO_DIR}/include")
    set (3RD_PARTY_LIBCOPP_LINK_NAME copp cotask)
    add_subdirectory(${3RD_PARTY_LIBCOPP_REPO_DIR})

    EchoWithColor(COLOR GREEN "-- Dependency: libcopp submodule found.(repository=${3RD_PARTY_LIBCOPP_REPO_DIR})")
else()
    EchoWithColor(COLOR RED "-- Dependency: libcopp is required")
    message(FATAL_ERROR "libcopp not found")
endif()

if (TARGET libcopp::copp OR TARGET libcopp::cotask)
    list(APPEND 3RD_PARTY_PUBLIC_LINK_NAMES libcopp::copp libcopp::cotask)
elseif (TARGET copp OR TARGET cotask)
    list(APPEND 3RD_PARTY_PUBLIC_LINK_NAMES copp cotask)
else ()
    list(APPEND 3RD_PARTY_PUBLIC_INCLUDE_DIRS ${3RD_PARTY_LIBCOPP_INC_DIR})
    list(APPEND 3RD_PARTY_PUBLIC_LINK_NAMES ${Libcopp_LIBRARIES} ${Libcotask_LIBRARIES})
endif ()
