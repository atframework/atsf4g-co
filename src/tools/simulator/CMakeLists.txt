# ============ simulator - [...] ============
get_filename_component(SIMULATOR_SRC_BIN_NAME ${CMAKE_CURRENT_LIST_DIR} NAME_WE)
set(SIMULATOR_SRC_BIN_NAME "${SIMULATOR_SRC_BIN_NAME}-cli")
echowithcolor(COLOR GREEN "-- Configure ${SIMULATOR_SRC_BIN_NAME} on ${CMAKE_CURRENT_LIST_DIR}")

aux_source_directory(${CMAKE_CURRENT_LIST_DIR} SIMULATOR_SRC_LIST)
file(MAKE_DIRECTORY "${PROJECT_INSTALL_BAS_DIR}/tools/simulator/bin")

unset(SIMULATOR_PRIVATE_DEFINITIONS)
unset(SIMULATOR_PRIVATE_LINK_NAMES)
unset(SIMULATOR_PRIVATE_INCLUDE_DIRS)

set(SRC_LIST "${CMAKE_CURRENT_LIST_DIR}/main.cpp")
# build libserver_frame.so
file(GLOB SRC_LIST_LIBSIMULATOR_UV ${CMAKE_CURRENT_LIST_DIR}/libsimulator_uv/*.h
     ${CMAKE_CURRENT_LIST_DIR}/libsimulator_uv/*.c ${CMAKE_CURRENT_LIST_DIR}/libsimulator_uv/*.cpp)
list(APPEND SRC_LIST ${SRC_LIST_LIBSIMULATOR_UV})

file(GLOB SRC_LIST_UTILITY ${CMAKE_CURRENT_LIST_DIR}/utility/*.h ${CMAKE_CURRENT_LIST_DIR}/utility/*.cpp)
list(APPEND SRC_LIST ${SRC_LIST_UTILITY})

file(GLOB_RECURSE SRC_LIST_PROTO ${CMAKE_CURRENT_LIST_DIR}/proto/*.h ${CMAKE_CURRENT_LIST_DIR}/proto/*.cpp)
list(APPEND SRC_LIST ${SRC_LIST_PROTO})

file(GLOB_RECURSE SRC_LIST_RPC ${CMAKE_CURRENT_LIST_DIR}/rpc/*.h ${CMAKE_CURRENT_LIST_DIR}/rpc/*.cpp)
list(APPEND SRC_LIST ${SRC_LIST_RPC})

# these service will use atgateway protocol
list(APPEND SIMULATOR_PRIVATE_INCLUDE_DIRS ${CMAKE_CURRENT_LIST_DIR} "${CMAKE_CURRENT_LIST_DIR}/libsimulator_uv")

if(ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_LUA_LINK_NAME)
  list(APPEND SIMULATOR_PRIVATE_LINK_NAMES ${ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_LUA_LINK_NAME})
  list(APPEND SIMULATOR_PRIVATE_DEFINITIONS SIMULATOR_ENABLE_LUA=1)

  file(GLOB_RECURSE SRC_LIST_LUA ${CMAKE_CURRENT_LIST_DIR}/lua/*.h ${CMAKE_CURRENT_LIST_DIR}/lua/*.cpp)
  list(APPEND SRC_LIST ${SRC_LIST_LUA})
  echowithcolor(COLOR GREEN "-- Install: Copy ${CMAKE_CURRENT_LIST_DIR}/lua/lua ...")
  file(
    COPY "${CMAKE_CURRENT_LIST_DIR}/lua/lua"
    DESTINATION "${PROJECT_INSTALL_BAS_DIR}/tools/simulator/bin/"
    USE_SOURCE_PERMISSIONS)
endif()

source_group(TREE "${CMAKE_CURRENT_LIST_DIR}" FILES ${SRC_LIST})

add_executable(${SIMULATOR_SRC_BIN_NAME} ${SRC_LIST})
project_tool_split_target_debug_sybmol("${SIMULATOR_SRC_BIN_NAME}")
project_tool_set_target_runtime_output_directory("${PROJECT_INSTALL_BAS_DIR}/tools/simulator/bin"
                                                 ${SIMULATOR_SRC_BIN_NAME} WITH_TARGET_RPATH WITH_ARCHIVE_RPATH)

target_link_libraries(${SIMULATOR_SRC_BIN_NAME} ${PROJECT_SERVER_FRAME_LIB_LINK} atgw_v1_c
                      ${SIMULATOR_PRIVATE_LINK_NAMES})

if(SIMULATOR_PRIVATE_DEFINITIONS)
  target_compile_definitions(${SIMULATOR_SRC_BIN_NAME} PRIVATE ${SIMULATOR_PRIVATE_DEFINITIONS})
endif()

target_include_directories(${SIMULATOR_SRC_BIN_NAME} PRIVATE "$<BUILD_INTERFACE:${SIMULATOR_PRIVATE_INCLUDE_DIRS}>")

set_property(TARGET ${SIMULATOR_SRC_BIN_NAME} PROPERTY FOLDER "${PROJECT_NAME}/tools")
if(MSVC)
  add_target_properties(${SIMULATOR_SRC_BIN_NAME} LINK_FLAGS /NODEFAULTLIB:library)
endif()

project_install_and_export_targets(${SIMULATOR_SRC_BIN_NAME})
