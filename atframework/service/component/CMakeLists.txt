set(ATSF4G_APP_NAME ${ATFRAMEWORK_SERVICE_COMPONENT_LINK_NAME})
echowithcolor(COLOR GREEN "-- Configure ${ATSF4G_APP_NAME} on ${CMAKE_CURRENT_LIST_DIR}")

set(ATFRAMEWORK_SERVICE_COMPONENT_VERSION "0.1.0")

set(ATFRAMEWORK_SERVICE_COMPONENT_GENERATED_DIR "${CMAKE_CURRENT_BINARY_DIR}/_generated")
file(MAKE_DIRECTORY "${ATFRAMEWORK_SERVICE_COMPONENT_GENERATED_DIR}/include/config")
file(MAKE_DIRECTORY "${ATFRAMEWORK_SERVICE_COMPONENT_GENERATED_DIR}/temp")

configure_file("${CMAKE_CURRENT_LIST_DIR}/config/atframe_services_build_feature.h.in"
               "${ATFRAMEWORK_SERVICE_COMPONENT_GENERATED_DIR}/temp/atframe_services_build_feature.h" @ONLY)
execute_process(
  COMMAND
    "${CMAKE_COMMAND}" -E copy_if_different
    "${ATFRAMEWORK_SERVICE_COMPONENT_GENERATED_DIR}/temp/atframe_services_build_feature.h"
    "${ATFRAMEWORK_SERVICE_COMPONENT_GENERATED_DIR}/include/config")

# ============ atproxy ============

file(
  GLOB_RECURSE
  ATSF4G_APP_SRC_LIST
  *.cpp
  *.cc
  *.c
  *.cxx
  *.h
  *.hpp)
source_group_by_dir(ATSF4G_APP_SRC_LIST)

if(BUILD_SHARED_LIBS OR ATFRAMEWORK_USE_DYNAMIC_LIBRARY)
  add_library(${ATSF4G_APP_NAME} SHARED ${ATSF4G_APP_SRC_LIST})
  set_target_properties(
    ${ATSF4G_APP_NAME}
    PROPERTIES C_VISIBILITY_PRESET "hidden"
               CXX_VISIBILITY_PRESET "hidden"
               VERSION ${ATFRAMEWORK_SERVICE_COMPONENT_VERSION}
               SOVERSION ${ATFRAMEWORK_SERVICE_COMPONENT_VERSION}
               INTERFACE_COMPILE_DEFINITIONS ATFRAME_SERVICE_COMPONENT_MACRO_API_DLL=1)
  target_compile_definitions(${ATSF4G_APP_NAME} PRIVATE ATFRAME_SERVICE_COMPONENT_MACRO_API_NATIVE=1
                                                        ATFRAME_SERVICE_COMPONENT_MACRO_API_DLL=1)
  if(MSVC)
    add_target_properties(${ATSF4G_APP_NAME} LINK_FLAGS /NODEFAULTLIB:library)
  endif(MSVC)
else()
  add_library(${ATSF4G_APP_NAME} STATIC ${ATSF4G_APP_SRC_LIST})
  set_target_properties(
    ${ATSF4G_APP_NAME}
    PROPERTIES C_VISIBILITY_PRESET "hidden"
               CXX_VISIBILITY_PRESET "hidden"
               VERSION ${ATFRAMEWORK_SERVICE_COMPONENT_VERSION})
  target_compile_definitions(${ATSF4G_APP_NAME} PRIVATE ATFRAME_SERVICE_COMPONENT_MACRO_API_NATIVE=1)
endif()

set_property(TARGET ${ATSF4G_APP_NAME} PROPERTY FOLDER "atframework/service")

target_link_libraries(
  ${ATSF4G_APP_NAME} PUBLIC ${ATFRAMEWORK_ATFRAME_UTILS_LINK_NAME} ${ATFRAMEWORK_LIBATBUS_LINK_NAME}
                            ${ATFRAMEWORK_LIBATAPP_LINK_NAME} ${PROJECT_THIRD_PARTY_PUBLIC_LINK_NAMES})

target_compile_options(${ATSF4G_APP_NAME} PRIVATE ${ATFRAMEWORK_SREVICE_PRIVATE_OPTIONS})

target_include_directories(
  ${ATSF4G_APP_NAME}
  PUBLIC "$<BUILD_INTERFACE:${PROJECT_THIRD_PARTY_PUBLIC_INCLUDE_DIRS}>"
         "$<BUILD_INTERFACE:${ATFRAMEWORK_SERVICE_COMPONENT_GENERATED_DIR}/include>"
         "$<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}>")

install(
  TARGETS ${ATSF4G_APP_NAME}
  EXPORT "${ATSF4G_APP_NAME}-target"
  RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
  LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
  ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR})

export(
  TARGETS ${ATSF4G_APP_NAME}
  NAMESPACE "atframework::"
  FILE "${CMAKE_LIBRARY_OUTPUT_DIRECTORY}/cmake/${ATSF4G_APP_NAME}/${ATSF4G_APP_NAME}-target.cmake")

install(
  EXPORT "${ATSF4G_APP_NAME}-target"
  NAMESPACE "atframework::"
  DESTINATION "${CMAKE_INSTALL_LIBDIR}/cmake/${ATSF4G_APP_NAME}")

install(FILES "${ATFRAMEWORK_SERVICE_COMPONENT_GENERATED_DIR}/include/config/atframe_services_build_feature.h"
        DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}/config/")
