set(ATSF4G_APP_NAME ${ATFRAMEWORK_SERVICE_COMPONENT_LINK_NAME})
echowithcolor(COLOR GREEN "-- Configure ${ATSF4G_APP_NAME} on ${CMAKE_CURRENT_LIST_DIR}")

set(ATFRAMEWORK_SERVICE_COMPONENT_GENERATED_DIR "${CMAKE_CURRENT_BINARY_DIR}/_generated")
file(MAKE_DIRECTORY "${ATFRAMEWORK_SERVICE_COMPONENT_GENERATED_DIR}/include/config")
file(MAKE_DIRECTORY "${ATFRAMEWORK_SERVICE_COMPONENT_GENERATED_DIR}/temp")

configure_file("${CMAKE_CURRENT_LIST_DIR}/config/atframe_services_build_feature.h.in"
               "${ATFRAMEWORK_SERVICE_COMPONENT_GENERATED_DIR}/temp/atframe_services_build_feature.h" @ONLY)
execute_process(
  COMMAND
    "${CMAKE_COMMAND}" -E copy_if_different
    "${ATFRAMEWORK_SERVICE_COMPONENT_GENERATED_DIR}/temp/atframe_services_build_feature.h"
    "${ATFRAMEWORK_SERVICE_COMPONENT_GENERATED_DIR}/include/config")

# ============ atproxy ============

file(GLOB_RECURSE ATSF4G_APP_SOURCE_LIST *.cpp *.cc *.c *.cxx)
file(GLOB_RECURSE ATSF4G_APP_HEADER_LIST *.h *.hpp)

atframework_add_library(
  ${ATSF4G_APP_NAME}
  FOLDER_PATH
  "atframework/sdk"
  PUBLIC_INCLUDE_DIRECTORY
  "${CMAKE_CURRENT_LIST_DIR}"
  "${ATFRAMEWORK_SERVICE_COMPONENT_GENERATED_DIR}/include"
  PUBLIC_LINK_NAMES
  ${ATFRAMEWORK_ATFRAME_UTILS_LINK_NAME}
  ${ATFRAMEWORK_LIBATBUS_LINK_NAME}
  ${ATFRAMEWORK_LIBATAPP_LINK_NAME}
  ${PROJECT_THIRD_PARTY_PUBLIC_LINK_NAMES}
  GENERATED_DIR
  "${ATFRAMEWORK_SERVICE_COMPONENT_GENERATED_DIR}/include"
  HEADERS
  ${ATSF4G_APP_HEADER_LIST}
  SOURCES
  ${ATSF4G_APP_SOURCE_LIST}
  GENERATED_HEADERS
  "${ATFRAMEWORK_SERVICE_COMPONENT_GENERATED_DIR}/include/config/atframe_services_build_feature.h"
  INSTALL_HEADER_DIRECTORY
  ".")

if(BUILD_SHARED_LIBS OR ATFRAMEWORK_USE_DYNAMIC_LIBRARY)
  target_compile_definitions(${ATSF4G_APP_NAME} PRIVATE ATFRAME_SERVICE_COMPONENT_MACRO_API_NATIVE=1
                                                        ATFRAME_SERVICE_COMPONENT_MACRO_API_DLL=1)
else()
  target_compile_definitions(${ATSF4G_APP_NAME} PRIVATE ATFRAME_SERVICE_COMPONENT_MACRO_API_NATIVE=1)
endif()

atframework_install_files(
  ${ATSF4G_APP_NAME} INSTALL_FILES
  "${ATFRAMEWORK_SERVICE_COMPONENT_GENERATED_DIR}/include/config/atframe_services_build_feature.h" INSTALL_DESTINATION
  "${CMAKE_INSTALL_INCLUDEDIR}/config")
