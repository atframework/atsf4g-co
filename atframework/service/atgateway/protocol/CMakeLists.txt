include(GNUInstallDirs)
include(CMakePackageConfigHelpers)

echowithcolor(COLOR GREEN "-- Configure ${ATSF4G_APP_NAME} on ${CMAKE_CURRENT_LIST_DIR}")
if(NOT ATFRAMEWORK_EXPORT_TARGET_NAME)
  set(ATFRAMEWORK_EXPORT_TARGET_NAME "atframework-target")
endif()

# ============ atgateway ============
if(NOT PROJECT_GENERATED_DIR)
  set(PROJECT_GENERATED_DIR "${CMAKE_BINARY_DIR}/_generated")
endif()
set(ATGATEWAY_GENERATED_SERVER_SDK_DIR "${PROJECT_GENERATED_DIR}/service/atgateway/server_sdk")
set(ATGATEWAY_GENERATED_CLIENT_SDK_DIR "${PROJECT_GENERATED_DIR}/service/atgateway/client_sdk")
set(ATGATEWAY_GENERATED_SERVICE_DIR "${PROJECT_GENERATED_DIR}/service/atgateway/service")
file(MAKE_DIRECTORY "${ATGATEWAY_GENERATED_SERVER_SDK_DIR}/atgateway/protocol")
file(MAKE_DIRECTORY "${ATGATEWAY_GENERATED_CLIENT_SDK_DIR}/atgateway/protocol/v2")
file(MAKE_DIRECTORY "${ATGATEWAY_GENERATED_SERVICE_DIR}/atgateway/protocol")
set(ATSF4G_APP_SDK_LIBATGW_SERVER_SDK_TARGET_NAME ${ATFRAMEWORK_SERVICE_GATEWAY_SERVER_SDK})
set(ATSF4G_APP_SDK_LIBATGW_PROTOCOL_SDK_TARGET_NAME ${ATFRAMEWORK_SERVICE_GATEWAY_CLIENT_SDK})
set(ATSF4G_APP_SDK_LIBATGW_SERVER_CONFIG_TARGET_NAME atgateway-server-config)

set(ATSF4G_APP_SDK_LIBATGW_PROTOCOL_API_HEADER_LIST
    "atgateway/protocol/v2/libatgw_protocol_sdk.h" "atgateway/protocol/v2/libatgw_protocol_config.h"
    "atgateway/protocol/libatgw_protocol_api.h")

set(ATSF4G_APP_SDK_LIBATGW_PROTOCOL_API_GENERATED_HEADER_LIST
    "${ATGATEWAY_GENERATED_CLIENT_SDK_DIR}/atgateway/protocol/v2/libatgw_protocol_sdk_generated.h")

set(ATSF4G_APP_SDK_LIBATGW_PROTOCOL_API_SOURCE_LIST "atgateway/protocol/v2/libatgw_protocol_sdk.cpp"
                                                    "atgateway/protocol/libatgw_protocol_api.cpp")

set(CMAKE_RUNTIME_OUTPUT_DIRECTORY "${PROJECT_INSTALL_BAS_DIR}/atframework/${ATSF4G_APP_NAME}/bin")

if(NOT EXISTS ${CMAKE_RUNTIME_OUTPUT_DIRECTORY})
  file(MAKE_DIRECTORY ${CMAKE_RUNTIME_OUTPUT_DIRECTORY})
endif()

# ============ libatbus - src ============
if(ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_FLATC_EXECUTABLE)
  set(ATFRAMEWORK_LIBATGW_PROTOCOL_SDK_FLAC_OPTIONS "${ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_FLATC_EXECUTABLE}" --cpp
                                                    --allow-non-utf8 --gen-mutable --scoped-enums)
  if(CMAKE_CXX_COMPILER_ID MATCHES "AppleClang|Clang")
    list(APPEND ATFRAMEWORK_LIBATGW_PROTOCOL_SDK_FLAC_OPTIONS --gen-nullable)
  endif()
  if(COMPILER_OPTION_RECOMMEND_CXX_STANDARD GREATER_EQUAL 17)
    list(APPEND ATFRAMEWORK_LIBATGW_PROTOCOL_SDK_FLAC_OPTIONS --cpp-std c++17 --cpp-static-reflection)
  endif()
  list(
    APPEND
    ATFRAMEWORK_LIBATGW_PROTOCOL_SDK_FLAC_OPTIONS
    -o
    "${CMAKE_CURRENT_BINARY_DIR}"
    -I
    "${CMAKE_CURRENT_LIST_DIR}"
    "${CMAKE_CURRENT_LIST_DIR}/atgateway/protocol/v2/libatgw_protocol_sdk.fbs")
  add_custom_command(
    OUTPUT ${ATSF4G_APP_SDK_LIBATGW_PROTOCOL_API_GENERATED_HEADER_LIST}
    COMMAND ${ATFRAMEWORK_LIBATGW_PROTOCOL_SDK_FLAC_OPTIONS}
    COMMAND "${CMAKE_COMMAND}" "-E" "copy_if_different" "${CMAKE_CURRENT_BINARY_DIR}/libatgw_protocol_sdk_generated.h"
            "${ATGATEWAY_GENERATED_CLIENT_SDK_DIR}/atgateway/protocol/v2/"
    DEPENDS "${CMAKE_CURRENT_LIST_DIR}/atgateway/protocol/v2/libatgw_protocol_sdk.fbs"
    COMMENT "Generate libatgw_protocol_sdk_generated.h")
  add_custom_target(${ATSF4G_APP_SDK_LIBATGW_PROTOCOL_SDK_TARGET_NAME}-generated
                    DEPENDS ${ATSF4G_APP_SDK_LIBATGW_PROTOCOL_API_GENERATED_HEADER_LIST})
endif()

# ATSF4G_APP_SDK_LIBATGW_SERVER_CONFIG_TARGET_NAME
if(EXISTS "${CMAKE_CURRENT_LIST_DIR}/atgateway/protocol/libatgw_server_config.proto" AND TARGET "atapp-protocol")
  atframework_add_protobuf_library(
    "${ATSF4G_APP_SDK_LIBATGW_SERVER_CONFIG_TARGET_NAME}"
    "${CMAKE_CURRENT_LIST_DIR}"
    NO_INSTALL_PBFILE
    NO_INSTALL_HEADERS
    ENABLE_PUBLIC_PRECOMPILE_HEADERS
    PUBLIC_SYMBOL_DECL
    "LIBATGW_SERVER_CONFIG_API"
    FOLDER_PATH
    "atframework/service/${ATSF4G_APP_NAME}"
    OUTPUT_DIR
    "${CMAKE_BINARY_DIR}/_generated/atgateway/config"
    PUBLIC_LINK_NAMES
    "atapp-protocol"
    COMPONENT_NAME
    "${ATSF4G_APP_NAME}"
    PROTOCOLS
    "${CMAKE_CURRENT_LIST_DIR}/atgateway/protocol/libatgw_server_config.proto")
endif()

# ATSF4G_APP_SDK_LIBATGW_PROTOCOL_SDK_TARGET_NAME
atframework_add_library(
  ${ATSF4G_APP_SDK_LIBATGW_PROTOCOL_SDK_TARGET_NAME}
  PUBLIC_SYMBOL_DECL
  LIBATGW_PROTOCOL_API
  FOLDER_PATH
  "atframework/atgateway/sdk"
  PUBLIC_INCLUDE_DIRECTORY
  "${ATGATEWAY_GENERATED_CLIENT_SDK_DIR}"
  "${CMAKE_CURRENT_LIST_DIR}"
  PUBLIC_LINK_NAMES
  ${ATFRAMEWORK_ATFRAME_UTILS_LINK_NAME}
  ${ATFRAMEWORK_LIBATBUS_LINK_NAME}
  ${ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_FLATBUFFERS_LINK_NAME}
  ${ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_PROTOBUF_LINK_NAME}
  GENERATED_DIR
  "${ATGATEWAY_GENERATED_CLIENT_SDK_DIR}"
  HEADERS
  ${ATSF4G_APP_SDK_LIBATGW_PROTOCOL_API_HEADER_LIST}
  SOURCES
  ${ATSF4G_APP_SDK_LIBATGW_PROTOCOL_API_SOURCE_LIST}
  GENERATED_HEADERS
  ${ATSF4G_APP_SDK_LIBATGW_PROTOCOL_API_GENERATED_HEADER_LIST})

if(TARGET ${ATSF4G_APP_SDK_LIBATGW_PROTOCOL_SDK_TARGET_NAME}-generated)
  add_dependencies(${ATSF4G_APP_SDK_LIBATGW_PROTOCOL_SDK_TARGET_NAME}
                   ${ATSF4G_APP_SDK_LIBATGW_PROTOCOL_SDK_TARGET_NAME}-generated)
endif()

if(COMPILER_OPTION_RECOMMEND_CXX_STANDARD GREATER_EQUAL 17)
  target_compile_definitions(${ATSF4G_APP_SDK_LIBATGW_PROTOCOL_SDK_TARGET_NAME}
                             PUBLIC ATFRAMEWORK_GATEWAY_MACRO_FLATC_USE_SCOPE_ENUM=1)
endif()

atframework_install_files(
  ${ATSF4G_APP_SDK_LIBATGW_PROTOCOL_SDK_TARGET_NAME}
  INSTALL_FILES
  "${CMAKE_CURRENT_LIST_DIR}/atgateway/protocol/v2/libatgw_protocol_config.h"
  "${CMAKE_CURRENT_LIST_DIR}/atgateway/protocol/v2/libatgw_protocol_sdk.h"
  "${CMAKE_CURRENT_LIST_DIR}/atgateway/protocol/v2/libatgw_protocol_sdk.fbs"
  INSTALL_DESTINATION
  "${CMAKE_INSTALL_DATADIR}/atgateway/protocol/v2/")

atframework_install_files(
  ${ATSF4G_APP_SDK_LIBATGW_PROTOCOL_SDK_TARGET_NAME} INSTALL_FILES
  "${CMAKE_CURRENT_LIST_DIR}/atgateway/protocol/libatgw_protocol_api.h" INSTALL_DESTINATION
  "${CMAKE_INSTALL_DATADIR}/atgateway/protocol/")

# ATSF4G_APP_SDK_LIBATGW_SERVER_SDK_TARGET_NAME-protocol
atframework_add_protobuf_library(
  "${ATSF4G_APP_SDK_LIBATGW_SERVER_SDK_TARGET_NAME}-protocol"
  "${CMAKE_CURRENT_LIST_DIR}"
  ENABLE_PUBLIC_PRECOMPILE_HEADERS
  PUBLIC_SYMBOL_DECL
  "LIBATGW_SERVER_API"
  FOLDER_PATH
  "atframework/service/${ATSF4G_APP_NAME}"
  OUTPUT_DIR
  "${ATGATEWAY_GENERATED_SERVER_SDK_DIR}"
  COMPONENT_NAME
  "${ATSF4G_APP_SDK_LIBATGW_SERVER_SDK_TARGET_NAME}"
  PROTOCOLS
  "${CMAKE_CURRENT_LIST_DIR}/atgateway/protocol/libatgw_server_protocol.proto")

# ATSF4G_APP_SDK_LIBATGW_SERVER_SDK_TARGET_NAME
atframework_add_library(
  ${ATSF4G_APP_SDK_LIBATGW_SERVER_SDK_TARGET_NAME}
  PUBLIC_SYMBOL_DECL
  LIBATGW_SERVER_API
  FOLDER_PATH
  "atframework/atgateway/sdk"
  PUBLIC_INCLUDE_DIRECTORY
  "${ATGATEWAY_GENERATED_SERVER_SDK_DIR}"
  "${CMAKE_CURRENT_LIST_DIR}"
  PUBLIC_LINK_NAMES
  "${ATSF4G_APP_SDK_LIBATGW_SERVER_SDK_TARGET_NAME}-protocol"
  ${ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_PROTOBUF_LINK_NAME}
  ${ATSF4G_APP_SDK_LIBATGW_PROTOCOL_SDK_TARGET_NAME}
  GENERATED_DIR
  "${ATGATEWAY_GENERATED_SERVER_SDK_DIR}"
  HEADERS
  ${ATSF4G_APP_SDK_LIBATGW_PROTOCOL_SERVICE_SDK_HEADER_LIST})
